# rdf-insert.fbp

# Exports
INPORT=request.URL:ENDPOINT
INPORT=regroup.IN:GRAPH
INPORT=load.INPUT:JSONLD
OUTPORT=request.OUTPUT:OUTPUT

# Initial Information Packets (IIP)
'graph' -> GROUP regroup(groups/Regroup) OUT -> PROPERTY insertGraph(objects/InsertProperty)
'POST' -> METHOD request
'Content-Type=application/sparql-update' -> IN propStringToObject(adapters/PropStringToObject) OUT -> HEADERS request
'"INSERT DATA {\n{{#if graph}}GRAPH <{{{graph}}}>{\n{{/if}}\n{{#each tokens}}{{{this}}}{{/each}}\n{{#if graph}}\n}\n{{/if}}\n}"' -> IN parse(strings/ParseJson) OUT -> BODY request

# Node Edges
load OUTPUT -> INPUT ntriples OUTPUT -> IN insertGraph OUT -> INPUT request

# Error Handling
repeat(core/Repeat) OUT -> IN output(core/Output)
load(rdf-components/rdf-load) ERROR -> IN repeat
ntriples(rdf-components/rdf-ntriples) ERROR -> IN repeat
request(rdf-components/request-template) ERROR -> IN repeat
