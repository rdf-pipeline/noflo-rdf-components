# chcs-patient-jsonld.fbp

# Takes a patient ID and returns JSON-LD.

# Exports
INPORT=setPatientID.VALUE:PATIENT_ID
INPORT=setAuthority.VALUE:AUTHORITY
INPORT=setDataset.VALUE:DATASET
OUTPORT=parseJson.OUTPUT:OUTPUT

# Initial Information Packets (IIP)

#patientid: simply id, e.g., 2-1
'patientid' -> KEY setPatientID(rdf-components/object) OUTPUT -> PARAMETERS request

# authority host:port, e.g., localhost:8080
'authority' -> KEY setAuthority(rdf-components/object) OUTPUT -> PARAMETERS request

# dataset to query e.g.., chcs-nc
'dataset'   -> KEY setDataset(rdf-components/object) OUTPUT -> PARAMETERS request

'http://{+authority}/patient_graph{?dataset,datatype,patientid}' -> URL request

# Request parameters {'datatype': 'all'}
'datatype' -> KEY parameters(rdf-components/object)
'all' -> VALUE parameters(rdf-components/object) OUTPUT -> PARAMETERS request

# Request Headers {'Accept': 'application/json'}
'Accept' -> KEY headers(rdf-components/object)
'application/json' -> VALUE headers(rdf-components/object) OUTPUT -> HEADERS request

# With patientID execute request and parse the result as JSON
setPatientID OUTPUT -> INPUT request OUTPUT -> INPUT parseJson(rdf-components/parse-json)

# Error Handling echos errors to console.log
error(core/Repeat) OUT -> IN output(core/Output)
request(rdf-components/request-template) ERROR -> IN error
