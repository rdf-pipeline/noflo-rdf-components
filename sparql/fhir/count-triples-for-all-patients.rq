# File:  count-triples-for-all-patients.rq
# If you make changes to this query, please check them into
# github under noflo-rdf-components/sparql/fhir
###########################################################
#
# Count Triples for All Patients
#
# This queries uses metadata in the mGraph: graph to
# count patient graphs by FHIR resource type.

PREFIX meta: <urn:meta#>
PREFIX mGraph: <urn:local:mGraph>
PREFIX fhir: <http://hl7.org/fhir/>

select ?patientId (count(*) as ?nTriples) {
  select distinct ?patientId ?s ?p ?v {
      {
        select distinct ?resourceGraph ?patientId { 
        # bind( "2-1007041" as ?patientId ) .
        graph mGraph: { 
          ?resourceGraph a meta:Graph ;
            meta:patientId ?patientId ;
            meta:fhirResourceType ?anyFhirResourceType .
        } 
      }
    }
    graph ?resourceGraph {
      ?s ?p ?v .
    }
  }
}
group by ?patientId
order by desc(?nTriples)


