# File:  list-patients-with-no-fhir-medicationdispense.rq
# If you make changes to this query, please check them into
# github under noflo-rdf-components/sparql/fhir
###########################################################
#
# List Patients with no FHIR MedicationDispense
#
# This query depends on metadata in mGraph: 

PREFIX meta: <urn:meta#>
PREFIX mGraph: <urn:test:mGraph>
PREFIX fhir: <http://hl7.org/fhir/>

select distinct ?patientId {
 bind( fhir:MedicationDispense as ?fhirResourceType ) .
  {
  select distinct ?patientId { 
    graph mGraph: { 
      ?g1 a meta:Graph ;
        meta:patientId ?patientId .
    } }
  }
  filter not exists {
    graph mGraph: {
      ?g2 a meta:Graph ;
        meta:patientId ?patientId ;
        meta:fhirResourceType ?fhirResourceType .
    }
    graph ?g2 {
      ?resource a ?fhirResourceType .
    }
  }
}
order by ?patientId

