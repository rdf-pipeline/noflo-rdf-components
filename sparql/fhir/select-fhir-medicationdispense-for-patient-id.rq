# Select fhir:MedicationDispense data for a patient ID.
# The patient ID is hard-coded into the "bind" statement below.
#
# This query depends on metadata in the mGraph: graph.

PREFIX meta: <urn:meta#>
PREFIX mGraph: <urn:test:mGraph>
PREFIX fhir: <http://hl7.org/fhir/>

select distinct ?patientId ?medication ?whenHandedOver { {
# select * { {
  select distinct ?graph ?patientId { 
    bind( "1000919" as ?patientId ) .
    graph mGraph: { 
      ?graph a meta:Graph ;
        meta:patientId ?patientId ;
        meta:fhirResourceType fhir:MedicationDispense .
    } }
  }
  graph ?graph {
    ?med a fhir:MedicationDispense .
    # ?med fhir:MedicationDispense.identifier ?medDispenseId .
    # TODO: Get a printable fhir:MedicationDispense ID
    ?med fhir:MedicationDispense.patient ?patient .
    # TODO: Verify that fhir:MedicationDispense.patient is the same patient
    ?med fhir:MedicationDispense.whenHandedOver / fhir:value ?whenHandedOver .
    ?med fhir:MedicationDispense.medicationReference ?medRef .
    ?medRef fhir:Reference.display / fhir:value ?medication .
  }
}


